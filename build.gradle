plugins {
    id 'groovy'
    id 'idea' // optional (to generate IntelliJ IDEA project files)
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.1'

    implementation project(':ecco-service')
    if (use_py4j_parser.toBoolean()) {
        println 'include py4j dependency'
        implementation group: 'net.sf.py4j', name: 'py4j', version: '0.10.9.2'
    }
    if (use_graalvm_parser.toBoolean()) {
        println 'include graalvm dependency'
        implementation group: 'org.graalvm.sdk', name: 'polyglot-tck', version: '21.3.0'
    }


    implementation(localGroovy())
    testImplementation(platform('org.junit:junit-bom:5.9.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

ecco.adapter = true

sourceSets {
    main {
        if (!use_py4j_parser.toBoolean()) {
            java {
                exclude '**/py4j/**'
            }
        }
        if (!use_graalvm_parser.toBoolean()) {
            java {
                exclude '**/graalvm/**'
            }
            resources {
                exclude 'graalVM-config.properties'
            }
        }
    }
}


test {
    useJUnitPlatform()
    include '**java/at.jku.isse.ecco.adapter.python/*'

    testLogging {
        events "passed", "skipped", "failed"
    }
}

ospackage {
    requires('ecco-base', version, EQUAL)
    requires('ecco-service', version, EQUAL)
}